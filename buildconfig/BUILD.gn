# Global Config
config("global") {
  cflags = []
  cflags_c = []
  cflags_cc = []
  defines = []
  ldflags = []
  include_dirs = []

  # mono-repo
  include_dirs += [ "//" ]

  # c/c++ language version
  cflags_c += [ "-std=${std_c}" ]
  cflags_cc += [ "-std=${std_cc}" ]

  # build type
  if (build_type == "debug") {
    cflags += [
      "-g",
      "-O0",
      "-Wall",
      "-Wextra",
    ]
    defines += [ "DEBUG" ]
  } else if (build_type == "release") {
    cflags += [
      "-O3",
      "-Wall",
      "-Wextra",
      "-Werror",
    ]
    defines += [ "NDEBUG" ]
  } else {
    assert(false, "Unknown build type!")
  }

  # Disable exceptions.
  cflags_cc += [ "-fno-exceptions" ]
}

config("executable") {
  if (host_os == "linux") {
    ldflags = [
      # ld.so understands the string $ORIGIN (or equivalently ${ORIGIN}) in an rpath
      # specification (DT_RPATH or DT_RUNPATH) to mean the directory containing the
      # application executable.
      #"-Wl,-rpath-link=\$ORIGIN",
      "-Wl,-rpath=\$ORIGIN",
    ]
  }
}

config("shared_library") {
  if (host_os == "linux" || host_os == "macos") {
    cflags = [ "-fPIC" ]
  }
}

config("static_libaray") {
}
